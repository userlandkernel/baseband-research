/*
 * I am converting this grammar to Python. - nfd 11/Jul/2005
*/

/*
 * PUBLIC DOMAIN PCCTS-BASED C++ GRAMMAR (cplusplus.g, stat.g, expr.g)
 *
 * Authors: Sumana Srinivasan, NeXT Inc.;            sumana_srinivasan@next.com
 *          Terence Parr, Parr Research Corporation; parrt@parr-research.com
 *          Russell Quong, Purdue University;        quong@ecn.purdue.edu
 *
 * VERSION 1.2
 *
 * SOFTWARE RIGHTS
 *
 * This file is a part of the ANTLR-based C++ grammar and is free
 * software.  We do not reserve any LEGAL rights to its use or
 * distribution, but you may NOT claim ownership or authorship of this
 * grammar or support code.  An individual or company may otherwise do
 * whatever they wish with the grammar distributed herewith including the
 * incorporation of the grammar or the output generated by ANTLR into
 * commerical software.  You may redistribute in source or binary form
 * without payment of royalties to us as long as this header remains
 * in all source distributions.
 *
 * We encourage users to develop parsers/tools using this grammar.
 * In return, we ask that credit is given to us for developing this
 * grammar.  By "credit", we mean that if you incorporate our grammar or
 * the generated code into one of your programs (commercial product,
 * research project, or otherwise) that you acknowledge this fact in the
 * documentation, research report, etc....  In addition, you should say nice
 * things about us at every opportunity.
 *
 * As long as these guidelines are kept, we expect to continue enhancing
 * this grammar.  Feel free to send us enhancements, fixes, bug reports,
 * suggestions, or general words of encouragement at parrt@parr-research.com.
 * 
 * NeXT Computer Inc.
 * 900 Chesapeake Dr.
 * Redwood City, CA 94555
 * 12/02/1994
 * 
 * Restructured for public consumption by Terence Parr late February, 1995.
 *
 * DISCLAIMER: we make no guarantees that this grammar works, makes sense,
 *             or can be used to do anything useful.
 */
/* 2001-2002
 * Version 1.0
 * This C++ grammar file has been converted from PCCTS to run under 
 *  ANTLR to generate lexer and parser in C++ code by
 *  Jianguo Zuo and David Wigg at
 *  The Centre for Systems and Software Engineering
 *  London South Bank University
 *  London, UK.
 *
 */
/* 2003
 * Version 2.0 was published by David Wigg in September 2003
 */
/* 2004
 * Version 3.0 July 2004
 * This is version 3.0 of the C++ grammar definition for ANTLR to 
 *  generate lexer and parser in C++ code updated by
 *  David Wigg at
 *  The Centre for Systems and Software Engineering
 *  London South Bank University
 *  London, UK.
 *
 * wiggjd@bcs.ac.uk
 * blackse@lsbu.ac.uk
 *
 * See MyReadMe.txt for further information
 *
 * This file is best viewed in courier font with tabs set to 4 spaces
 */

header 
    {
    // The statements in this block appear in both CPPLexer.py and CPPParser.py
    from CPPDictionary import CPPDictionary
    from LineObject import LineObject
    import CPPSymbol
    from var_types import * # "Do not use. Should be deleted in the next version"
    from enum_types import *
    }

header "CPPLexer.__init__"
{
self.line_directive_handler = kwargs.get("line_directive_handler")
self.lineObject = LineObject()
self.originalSource = ""

self._line = 0
}

header "CPPParser.__init__"
{//antlrTrace(False);    // This is a dynamic trace facility for use with -traceParser etc.
/* It requires modification in LLkParser.cpp and LLkParser.hpp
otherwise it should be commented out (see MyReadMe.txt)
true shows antlr trace (can be set and reset during parsing)
false stops showing antlr trace 
Provided the parser is always generated with -traceParser this
facility allows trace output to be turned on or off by changing
the setting here from false to true or vice versa and then
recompiling and linking CPPParser only thus avoiding the need
to use antlr.Tool to re-generate the lexer and parser again. 
*/

// Creates a dictionary to hold symbols with 4001 buckets, 200 scopes and 800,000 characters
// These can be changed to suit the size of program(s) being parsed
// nfd: The Python version uses Python's native dict, which has "infinite" capacity, so
// the above params aren't passed in.
self.symbols = CPPDictionary()

// Set template parameter and external scopes
// Set template parameter scope to 0
self.templateParameterScope = self.symbols.getCurrentScopeIndex()
self.symbols.saveScope()    // Advance currentScope from 0 to 1
self.externalScope = self.symbols.getCurrentScopeIndex()    // Set external scope to 1

// Declare predefined scope "std" in external scope
a = CPPSymbol.CPPSymbol("std", CPPSymbol.otTypedef)
self.symbols.define("std", a)

// Global flags to allow for nested declarations
self._td = False        // For typedef
self._fd = False        // For friend
self._ts = None         // For TypeSpecifier
self._ds = dsInvalid    // For DeclSpecifier

// 0 = Function definition not being parsed
// 1 = Parsing function name
// 2 = Parsing function parameter list
// 3 = Parsing function block
self.functionDefinition = 0
self.qualifierPrefix = "" // [0] = '\0';
self.enclosingClass = "";
self.assign_stmt_RHS_found = 0;
self.in_parameter_list = False;
self.K_and_R = False;    // used to distinguish old K & R parameter definitions
self.in_return = False;
self.is_address = False;
self.is_pointer = False;

// "protected:"
}

options
	{
	language = "Python";
	}

{

//	The statements in this block appear only in CPPParser.py and not in CPPLexer.py

// Used to control selected (level) tracing (see support.cpp)
// 1 Shows which external and member statements selected
// 2 Shows above plus all declarations/definitions
// 3 reserved for future use
// 4 and above available for user
statementTrace = 0

def trace1(s):
    if statementTrace >= 1:
        print s

def checknode(node):
    assert node is not None
    return node

CPPParser_MaxQualifiedItemSize = 500


// Limit lookahead for qualifiedItemIs()
MaxTemplateTokenScan = 200 

}	// End of CPPParser.cpp block


class CPPParser extends Parser;

options
	{
	k = 2;
	exportVocab = STDC;
	buildAST = false;
	codeGenMakeSwitchThreshold = 2;
	codeGenBitsetTestThreshold = 3;
	}


translation_unit returns [node]
	{wt = self.LT(1)}
   :  {self.enterExternalScope()} {accum = []}
      (ed = external_declaration {accum.append(ed)})+  EOF {node = self.autonode(accum)}
      {self.exitExternalScope()}
   ;

external_declaration returns [node]
	{self.K_and_R = False; wt = self.LT(1)}
	:  
	(
	// Template explicit specialisation (DW 14/04/03)
	// Ref: http://msdn2.microsoft.com/library/c401y1kb(en-us,vs.80).aspx (nfd)
		("template" LESSTHAN GREATERTHAN)=>
		{trace1("%d external_declaration_0 template explicit-specialisation\n" %(self.LT(1).getLine()))}
		"template" LESSTHAN GREATERTHAN d = declaration
		{node = self.node("template_specialisation", [d])}
	|
	// Class definition (templates too)
	// This is separated out otherwise the next alternative
	// would look for "class A { ... } f() {...}" which is
	// an unacceptable level of backtracking.

	// JEL Note:  Rule body does not need typedef, because
	// that is internal to "declaration", and it is invalid
	// to say "typedef template..."
	 	// Class definition
		(("typedef")? class_head)=>
		{trace1("%d external_declaration_1a Class definition\n" % (self.LT(1).getLine()))}
		d = declaration
		{node = d}
	|	
		// Class template definition
		(template_head class_head)=>
		{trace1("%d external_declaration_1b Class template definition\n" % (self.LT(1).getLine()))}
		th = template_head d = declaration
		{node = self.node("template", [th, d])}
   |  
		// Enum definition (don't want to backtrack over this in other alts)
		("enum" (ID)? LCURLY)=>
		{trace1("%d external_declaration_2 Enum definition\n" % (self.LT(1).getLine()))}
		es = enum_specifier (idl = init_declarator_list)? SEMICOLON! {self.end_of_stmt()}
		{node = self.unfinishednode("enum")}
   |  
		// Destructor DEFINITION (templated or non-templated)
		((template_head)? dtor_head[1] LCURLY)=>
		{trace1("%d external_declaration_3 Destructor definition\n" % (self.LT(1).getLine()))}
		(th = template_head)? dh = dtor_head[1] db = dtor_body
		{node = self.unfinishednode("destructor")}
	|  
		// Constructor DEFINITION (non-templated)
		// JEL 4/3/96 Added predicate that works, once the
		// restriction is added that ctor cannot be virtual
		// and ctor_declarator uses a more restrictive id
		(	(options {warnWhenFollowAmbig = false;}:
			 cds = ctor_decl_spec)?
			{self.qualifiedItemIsOneOf(qiCtor)}?
		)=>
		{trace1("%d external_declaration_4 Constructor definition\n" % (self.LT(1).getLine()))}
		cd = ctor_definition
		{node = self.unfinishednode("non-templated constructor")}
   |  
		// User-defined type cast
		(("inline")? scope_override  conversion_function_decl_or_def)=>
		{trace1("%d external_declaration_5 Operator function\n" % (self.LT(1).getLine()))}
		("inline")? s = scope_override cfdd = conversion_function_decl_or_def 
		{node = self.unfinishednode("typecast")}
   |   
		// Function declaration
		(declaration_specifiers function_declarator[0] SEMICOLON!)=> 
		{trace1("%d external_declaration_6 Function declaration\n" % (self.LT(1).getLine()))}
		d = declaration
		{node = checknode(d)}
	|
		// Function definition
		(declaration_specifiers	function_declarator[1] LCURLY)=> 
		{trace1("%d external_declaration_7 Function definition\n" % (self.LT(1).getLine()))}
		fd = function_definition {node = fd}
	|
		// K & R Function definition
		(declaration_specifiers	function_declarator[1] declaration)=>
{
self.K_and_R = True
trace1("%d external_declaration_8 K & R function definition\n" % (self.LT(1).getLine()))
}
		fd = function_definition {node = fd}
	|  
		// templated forward class decl, init/decl of static member in template
		(template_head declaration_specifiers (init_declarator_list)? SEMICOLON! {self.end_of_stmt()})=>
{
self.beginTemplateDeclaration()
trace1("%d external_declaration_9 Class template declaration\n" % (self.LT(1).getLine()))
}
		th = template_head ds = declaration_specifiers (idl = init_declarator_list)? SEMICOLON! {self.end_of_stmt()}
		{self.endTemplateDeclaration()}
		{node = self.unfinishednode("templated_forward_class_decl")}
	|  
		// Templated FUNCTIONS and CONSTRUCTORS matched here.
		{self.beginTemplateDefinition()}
		th = template_head
		{node = self.node("templated_function")}
		(  
			// Templated CONSTRUCTOR definition
            // JEL 4/3/96 Added predicate that works once the
            // restriction is added that ctor cannot be virtual
			(	ctor_decl_spec 
				{self.qualifiedItemIsOneOf(qiCtor)}?
			)=>
			{trace1("%d external_declaration_10a Template constructor definition\n" % (self.LT(1).getLine()))}
			cd = ctor_definition {node.add_child(cd)}
		|
			// Templated function declaration
			(declaration_specifiers function_declarator[0] SEMICOLON!)=> 
			{trace1("%d external_declaration_10b Function template declaration\n" % (self.LT(1).getLine()))}
			d = declaration
			{node.add_child(d)}
		|  
			// Templated function definition
			(declaration_specifiers function_declarator[1] LCURLY)=> 
			{trace1("%d external_declaration_10c Function template definition\n" % (self.LT(1).getLine()))}
			fd = function_definition {node.add_child(fd)}
		)
		{self.endTemplateDefinition()}
	|  
		{trace1("%d external_declaration_11 Namespace declaration\n" % (self.LT(1).getLine()))}
		dn = decl_namespace
		{node = self.unfinishednode("namespace_decl")}
	|	
		// everything else (except templates)
		{trace1("%d external_declaration_12 Declaration\n" % (self.LT(1).getLine()))}
		d = declaration
		{node = d}
	|	
		{trace1("%d external_declaration_13 Semicolon\n" % (self.LT(1).getLine()))}
		SEMICOLON! {self.end_of_stmt()}
	)
	;	// end of external_declaration

decl_namespace returns [node]
	:	
		"namespace" 
		(
			{node = self.node("namespace")}
			(ns:ID {node.set_leaf(ns.getText())}
{
self._td = True
self.declaratorID(ns.getText(), qiType)
})?
			// The following statement can be invoked to trigger selective antlr trace
			// Also see below
			//{if (strcmp((ns->getText()).data(),"xyz")==0) antlrTrace(True);}	// Used for diagnostic trigger
			LCURLY 
			{self.enterNewLocalScope()}
			(ed = external_declaration {node.add_child(ed)} )*
			{self.exitLocalScope()}
			RCURLY
			// The following should be implemented to match the optional statement above
			//{antlrTrace(False);}
		|
			{node = self.node("namespace_assignment")}
			ns2:ID 
{
self._td = True
self.declaratorID(ns2.getText(), qiType)
}
			ASSIGNEQUAL qid = qualified_id SEMICOLON! {self.end_of_stmt()} 
			{node.set_leaf([ns2.getText(), qid])}
		)
	;

member_declaration returns [node]
	:
	(
		// Class definition
		// This is separated out otherwise the next alternative
		// would look for "class A { ... } f() {...}" which is
		// an unacceptable level of backtracking.
		( ("typedef")? class_head) => 
		{trace1("%d member_declaration_1 Class definition\n" % (self.LT(1).getLine()))}
		d = declaration
		{node = d}
	|  
		// Enum definition (don't want to backtrack over this in other alts)
		("enum" (ID)? LCURLY)=>
		{trace1("%d member_declaration_2 Enum definition\n" % (self.LT(1).getLine()))}
		{mdl = None}
		es = enum_specifier (mdl = member_declarator_list)? SEMICOLON!	{self.end_of_stmt()}
		{node = self.autonode([es, mdl])}
	|
		// Constructor declarator
		(	ctor_decl_spec
			{self.qualifiedItemIsOneOf(qiCtor)}?
			ctor_declarator[0] SEMICOLON!
		)=>
		{trace1("%d member_declaration_3 Constructor declarator\n" % (self.LT(1).getLine()))}
		cds = ctor_decl_spec 
		cd = ctor_declarator[0] 	SEMICOLON! {self.end_of_stmt()} // Constructor declarator
		{node = self.autonode([cds, cd])}
	|  
		// JEL Predicate to distinguish ctor from function
		// This works now that ctor cannot have VIRTUAL
		// It unfortunately matches A::A where A is not enclosing
		// class -- this will have to be checked semantically
		(	ctor_decl_spec
			{self.qualifiedItemIsOneOf(qiCtor)}?
			ctor_declarator[1]
			(COLON        // DEFINITION :ctor_initializer
			|LCURLY       // DEFINITION (compound Statement) ?
			)
		)=>
		{trace1("%d member_declaration_4 Constructor definition\n" % (self.LT(1).getLine()))}
		cd = ctor_definition 
		{node = self.autonode([cd])}
   |  
		// No template_head allowed for dtor member
		// Backtrack if not a dtor (no TILDE)
		(dtor_head[0] SEMICOLON!)=>
		{trace1("%d member_declaration_5a Destructor declaration\n" % (self.LT(1).getLine()))}
		dh = dtor_head[0] SEMICOLON! {self.end_of_stmt()}	// Declaration
		{node = self.autonode([dh])}
	|
		// No template_head allowed for dtor member
		// Backtrack if not a dtor (no TILDE)
		(dtor_head[1] LCURLY)=>
		{trace1("%d member_declaration_5b Destructor definition\n" % (self.LT(1).getLine()))}
		dh = dtor_head[1] db = dtor_body	// Definition
		{node = self.autonode([dh, db])}
	|
		// Function declaration
		(declaration_specifiers	function_declarator[0] SEMICOLON!)=>
		{trace1("%d member_declaration_6 Function declaration\n" % (self.LT(1).getLine()))}
		d = declaration
		{node = d}
	|  
		// Function definition
		(declaration_specifiers function_declarator[1] LCURLY)=>
{
self.beginFieldDeclaration()
trace1("%d member_declaration_7 Function definition\n" % (self.LT(1).getLine()))
}
		fd = function_definition
		{node = fd}
	|  
		// User-defined type cast
		(("inline")? conversion_function_decl_or_def)=>
		{trace1("%d member_declaration_8 Operator function\n" % (self.LT(1).getLine()))}
		("inline")? cfdd = conversion_function_decl_or_def
		{node = self.unfinishednode("udtc-439")}
	|  
		// Hack to handle decls like "superclass::member",
		// to redefine access to private base class public members
		(qualified_id SEMICOLON!)=>
		{trace1("%d member_declaration_9 Qualified ID\n" % (self.LT(1).getLine()))}
		q = qualified_id SEMICOLON! {self.end_of_stmt()}
		{node = self.unfinishednode("declhack-446")}
	|  
		// Member with a type or just a type def
		// A::T a(), ::T a, ::B a, void a, E a (where E is the enclosing class)
		(declaration_specifiers)=>
{
self.beginFieldDeclaration()
trace1("%d member_declaration_10 Declaration(s)\n" % (self.LT(1).getLine()))
}
		{mdl = []}
		ds = declaration_specifiers (md = member_declarator_list {mdl = [md]})? SEMICOLON! {self.end_of_stmt()}
		{node = self.autonode([ds] + mdl)}
	|  
		// Member without a type (I guess it can only be a function declaration or definition)
		(function_declarator[0] SEMICOLON!)=>
{
self.beginFieldDeclaration()
trace_err("%d warning Function declaration found without typename\n" % (self.LT(1).getLine()))
trace1("%d member_declaration_11a Function declaration\n" % (self.LT(1).getLine()))
}
		fd = function_declarator[0] SEMICOLON! {self.end_of_stmt()}
		{node = checknode(fd)}
	|
		// Member without a type (I guess it can only be a function definition)
{
trace_err("%d warning Function definition found without typename\n" % (self.LT(1).getLine()))
trace1("%d member_declaration_11b Function definition\n" % (self.LT(1).getLine()))
}
		fd = function_declarator[1] cs = compound_statement {self.endFunctionDefinition()}
		{node = self.autonode([fd, cs])}
	|  
		// templated forward class decl, init/decl of static member in template
		// DW 27/06/03 Copied here from external_declaration since templates can now be nested
		(template_head ds = declaration_specifiers (init_declarator_list)? SEMICOLON!)=>
{
self.beginTemplateDeclaration()
trace1("%d member_declaration_12 Class template declaration\n" % (self.LT(1).getLine()))
}
		 th = template_head ds = declaration_specifiers {node = self.autonode([th, ds])} (idl = init_declarator_list {node.add_child(idl)} )? SEMICOLON! {self.end_of_stmt()}
		{self.endTemplateDeclaration()}
	|  
		// Templated FUNCTIONS and CONSTRUCTORS matched here.
		// DW 27/06/03 Copied here from external_declaration since templates can now be nested
		{self.beginTemplateDefinition()}
		th = template_head {node = self.node("templated_function", [th])}
		(
			// Templated CONSTRUCTOR definition
			// JEL 4/3/96 Added predicate that works once the
			// restriction is added that ctor cannot be virtual
			(ctor_decl_spec
			 {self.qualifiedItemIsOneOf(qiCtor)}?
			)=>
			{trace1("%d member_declaration_13a Template constructor definition\n" % (self.LT(1).getLine()))}
			cd = ctor_definition {node.add_child(cd)}
		|
			// Templated function declaration
			(declaration_specifiers function_declarator[0] SEMICOLON!)=>
			{trace1("%d member_declaration_13b Function template declaration\n" % (self.LT(1).getLine()))}
			d = declaration {node.add_child(d)}
		|  
			// Templated function definition
			// Function definition DW 2/6/97
			(declaration_specifiers function_declarator[1] LCURLY)=> 
			{trace1("%d member_declaration_13c Function template definition\n" % (self.LT(1).getLine()))}
			fd = function_definition {node.add_child(fd)}
		|
			{trace1("%d member_declaration_13d Templated operator function\n" % (self.LT(1).getLine()))}
			cfdd = conversion_function_decl_or_def {node.add_child(cfdd)}
		)
		{self.endTemplateDefinition()}
	|  
		{trace1("%d member_declaration_14 Access specifier\n" % (self.LT(1).getLine()))}
		as = access_specifier COLON
		{node = self.node(as)}
	|  
		{trace1("%d member_declaration_15 Semicolon\n" % (self.LT(1).getLine()))}
		SEMICOLON! {self.end_of_stmt()}
		{node = self.unfinishednode("member-decl-524")}
	)
	;	// end member_declaration

function_definition returns [node]
	:	// don't want next action as an init-action due to (...)=> caller
	{wt = self.LT(1); self.beginFunctionDefinition()}
	(	// Next line is equivalent to guarded predicate in PCCTS
		// (SCOPE | ID)? => <<self.qualifiedItemIsOneOf(qiType|qiCtor)>>?
		{( not (self.LA(1)==SCOPE or self.LA(1)==ID) or self.qualifiedItemIsOneOf(qiType|qiCtor) )}?
		ds = declaration_specifiers fd = function_declarator[1]
		{decl_node = self.node("declaration_list")}
		(	options{warnWhenFollowAmbig = false;}:
			(d = declaration {decl_node.add_child(d)})*	// Possible for K & R definition
			{self.in_parameter_list = False}
		)?
		cs = compound_statement
		{node = self.autonode([ds, fd, decl_node, cs])}
	|	// Next line is equivalent to guarded predicate in PCCTS
		// (SCOPE | ID)? => <<self.qualifiedItemIsOneOf(qiPtrMember)>>?
		//{( !(self.LA(1)==SCOPE||self.LA(1)==ID) || (self.qualifiedItemIsOneOf(qiPtrMember)) )}?
		fd = function_declarator[1]
		{decl_node = self.node("declaration_list")}
		(	options{warnWhenFollowAmbig = false;}:
			(d = declaration)*	 {decl_node.add_child(d)}// Possible for K & R definition
			{self.in_parameter_list = False}
		)?
		cs = compound_statement
		{node = self.autonode([fd, decl_node, cs])}
	)
	{self.endFunctionDefinition()}
	;

declaration returns [node]
	{wt = self.LT(1)}
	:	
		("extern" StringLiteral)=>
		ls = linkage_specification {node = ls}
	|	
		{self.beginDeclaration()}
		// LL 31/1/97: added (COMMA) ? below. This allows variables to typedef'ed more than once. DW 18/08/03 ?
		ds = declaration_specifiers {node = self.autonode([ds])} ((COMMA)? (a = attribute)? idl = init_declarator_list {node.add_child(idl)} )? SEMICOLON! {self.end_of_stmt()}
		{self.endDeclaration()}
	|	
		ud = using_declaration	// DW 19/04/04
		{node = ud}
	;

linkage_specification returns[node]
	:	"extern" {node = self.autonode()}
		StringLiteral
		(LCURLY (ed = external_declaration {node.add_child(ed)})* RCURLY
		|d = declaration {node.add_child(d)}
		)
	;

declaration_specifiers returns [node]
{
self._td = False
self._fd = False
self._ts = None
self._ds = dsInvalid

td = False
fd = False
ts = None
ds = dsInvalid
tq = None
sc = None

wt = self.LT(1)
}
	:
	{node = self.node("declaration_specifiers")}
	(	(options {warnWhenFollowAmbig = false;}
		:	sc = storage_class_specifier {node.add_child(self.node(sc))}
		|	tq = type_qualifier {node.add_child(self.node(tq))}
		|   "__extension__" /* nfd: gcc extension */
		|	("inline"^|"_inline"^|"__inline"^|"__inline__"^)
				{ds = dsINLINE} {node.add_child(self.node("inline"))}
		|	"virtual"^						{ds = dsVIRTUAL} {node.add_child(self.node("virtual"))}
		|	"explicit"^						{ds = dsEXPLICIT} {node.add_child(self.node("explicit"))}
		|	"typedef"^	{td=True} {node.add_child(self.node("typedef"))}
		|	"friend"^	{fd=True} {node.add_child(self.node("friend"))}
		|	("_stdcall"^|"__stdcall"^) {node.add_child(self.node("stdcall"))}
		)*
		ts = type_specifier[ds] {node.add_child(ts)}
	|	
		"typename"	{td=True;}
		dd = direct_declarator 
		{node = self.autonode([dd], ["typename"])}
	)
	{self.declarationSpecifier(td,fd,sc,tq,ts,ds)}
	;

attribute returns[a]  /* nfd */
	: "__attribute__" LPAREN LPAREN attribute_decl_list RPAREN RPAREN
		{a = None}
	;

attribute_decl_list
	: "__mode__" LPAREN ID RPAREN
	| "const"
	| "__const" /* nfd: gcc extension */
	| "__noreturn__"
	| "__returns_twice__"
	| "__noinline__"
	| "__always_inline__"
	| "__flatten__"
	| "__pure__"
	| "__const__"
	| "__nothrow__"
	| "__sentinel__"
	| "__format__" LPAREN ID COMMA c1=constant COMMA c2=constant RPAREN
	| "__format_arg__"
	| "__no_instrument_function__"
	| "__section__"
	| "__constructor__"
	| "__destructor__"
	| "__used__"
	| "__unused__"
	| "__deprecated__"
	| "__weak__"
	| "__malloc__"
	| "__alias__"
	| "__warn_unused_result__"
	| "__nonnull__" LPAREN c1=constant (COMMA c2=constant)* RPAREN
	| "__externally_visible__"
	;

storage_class_specifier returns [sc]
	:	"auto"		{sc = "auto";}
	|	"register"	{sc = "register";}
	|	"static"	{sc = "static";}
	|	"extern"	{sc = "extern";}
	|	"mutable"	{sc = "mutable";}
	;

type_qualifier returns [tq] // aka cv_qualifier
	:  ("const"|"const_cast"|"__const")	{tq = "const";}  /* nfd: __const is a gcc extension */
	|  ("volatile"|"__volatile")				{tq = "volatile";} /* nfd: ditto __volatile */
	|  ("__restrict" | "__restrict__") {tq = "restrict"}
	;

type_specifier[ds] returns [ts]
	:	ts = simple_type_specifier
	|	ts = class_specifier[ds]
	|	ts = enum_specifier
	;

simple_type_specifier returns [node]
	{wt = self.LT(1); node = self.autonode()}
	:	(	{self.qualifiedItemIsOneOf(qiType|qiCtor)}? 
			 {wt = self.LT(1)}
			 s = qualified_type {node.add_child(self.node(s))}
		|	
			 {wt = self.LT(1)}
			(	"char"^		{node.add_child(self.node("char"))}
			/*|	"wchar_t"^	{ts.append("wchar_t")} */ /* FIXME: We're a half-C parser now */
			|	"bool"^		{node.add_child(self.node("bool"))}
			|	"short"^{node.add_child(self.node("short"))}
			|	"int"^	{node.add_child(self.node("int"))}
			|	("_int64"^|"__int64"^)	{node.add_child(self.node("__int64"))}
			|	"__w64"^		{node.add_child(self.node("long"))}
			|	"long"^		{node.add_child(self.node("long"))}
			/* nfd: GCC type aliases for "signed" */
			|	("signed"^|"__signed"^|"__signed__"^)	{node.add_child(self.node("signed"))}
			|	"unsigned"^	{node.add_child(self.node("unsigned"))}
			|	"float"^	{node.add_child(self.node("float"))}
			|	"double"^	{node.add_child(self.node("double"))}
			|	"void"^	{node.add_child(self.node("void"))}
			|	("_declspec"^|"__declspec"^) LPAREN ID RPAREN 
			|   "__builtin_va_list" {node.add_child(self.node("__builtin_va_list"))}
			)+
		|
		// Fix towards allowing us to parse *.cpp files directly
		(qualified_type qualified_id)=> s = qualified_type {node.add_child(self.node("typeid"))}
		{print("simple_type_specifier third option entered - %s" % (s))}
		)
	;

qualified_type returns [q]
	: 
		// JEL 3/29/96 removed this predicate and moved it upwards to
		// simple_type_specifier.  This was done to allow parsing of ~ID to 
		// be a unary_expression, which was never reached with this 
		// predicate on
		// {self.qualifiedItemIsOneOf(qiType|qiCtor)}?

		s = scope_override 
		id:ID^ 
		(options {warnWhenFollowAmbig = false;}:
		 LESSTHAN tal = template_argument_list GREATERTHAN
		)?
{
//printf("qualified_type entered\n");
q = s + id.getText()
}
	;

class_specifier[ds] returns [node]
	{wt = self.LT(1)}
	:	("class"^	{ts = "class"}	
		|"struct"^	{ts = "struct"}
		|"union"^	{ts = "union"}
		)
		(	id = qualified_id
			{bases = None}
			(options{generateAmbigWarnings = false;}:
{saveClass = self.enclosingClass
self.enclosingClass = id
}
				{bases = []} (bases = base_clause)?
				LCURLY	 
				{self.beginClassDefinition(ts, id)}	// This stores class name in dictionary
				{members = self.node("members")}
				(md = member_declaration {members.add_child(md)})*
				{self.endClassDefinition()}
				RCURLY
				{node = self.node(ts, bases + [members], id)}
				{self.enclosingClass = saveClass}
			|
				{node = self.node(ts, [], id)}
				{self.classForwardDeclaration(ts, ds, id)}
			)
		|
			LCURLY	 
{saveClass = self.enclosingClass
self.enclosingClass = "__anonymous"}
			{self.beginClassDefinition(ts, "anonymous")}
			{members = self.node("members")}
			(md = member_declaration {members.add_child(md)})*
			{self.endClassDefinition()}
			{node = self.node(ts, [members], None)}
			RCURLY
			{self.enclosingClass = saveClass}
		) 
                {node.set_result("")}      // FIXME: is this right?
	;

enum_specifier returns [node]
	:	"enum"
		{node = self.autonode()}
		(	LCURLY el = enumerator_list {node.add_child(el);}RCURLY
		|	id:ID     {node.set_leaf(id.getText());}
				// DW 22/04/03 Suggest qualified_id here to satisfy elaborated_type_specifier
			{self.beginEnumDefinition((id.getText()));}
			(LCURLY el = enumerator_list {node.add_child(el);} RCURLY)?
			{self.endEnumDefinition();}
		)
	;

/* nfd: Optional trailing comma is a GCC-ism (mar 14 2006)*/
enumerator_list returns[node]
	:	e = enumerator
		{node = self.autonode(); node.add_child(e)}
		/* nfd : it's always okay to choose this option when available,
		 * rather than the trailing comma and an error.
		*/
		( options {warnWhenFollowAmbig=false;} : COMMA e = enumerator {node.add_child(e)})*
		(COMMA)?
	;

enumerator returns[node]
	:	id:ID {node = self.autonode(); node.set_leaf(id.getText())}
		(ASSIGNEQUAL cx = constant_expression {node.add_child(cx)})?
		{self.enumElement(id.getText())}
	;

/* This matches a generic qualified identifier ::T::B::foo
 * (including OPERATOR).
 * It might be a good idea to put T::~dtor in here
 * as well, but id_expression in expr.g puts it in manually.
 * Maybe not, 'cause many people use this assuming only A::B.
 * How about a 'qualified_complex_id'?
 */
qualified_id returns [q]
	:
	so =  scope_override
	{qitem = so}
	(  
	id:ID	(options{warnWhenFollowAmbig = false;}:
			 LESSTHAN tal = template_argument_list GREATERTHAN)?
	{qitem += id.getText()}
	|  
	OPERATOR o = optor
	{qitem += "operator"; qitem += o}
	|
	"this"  // DW 21/07/03 fix to pass test8.i
	|
	("true"|"false")	// DW 21/07/03 fix to pass test8.i
	)
	{q = qitem}
	;

typeID
	:	{self.isTypeName(self.LT(1).getText())}?
		ID
	;

init_declarator_list returns [node]
	:	{wt = self.LT(1)} id = init_declarator {idl = [id]} (COMMA id = init_declarator {idl.append(id)})*
		(a = attribute)*
	{node = self.autonode(idl)}
	;

init_declarator returns [node]
	{wt = self.LT(1)}
	:	d = declarator {node = self.autonode([d])}
	    (an = func_asm_name {node.add_child(an)})?
		(	
			ASSIGNEQUAL 
			i = initializer
			{node.add_child(i); d.set_result(i.result)}
		|	
			LPAREN el = expression_list RPAREN
			{node.add_child(el); d.set_result(el.result)}
		)?
	;

initializer returns[node]
   {wt = self.LT(1)}
   :  rx = remainder_expression {node = checknode(rx)}// DW 18/4/01 assignment_expression
   |  LCURLY i1 = initializer {node = self.autonode([i1])} (COMMA i2 = initializer {node.add_child(i2)})* RCURLY
   |  d1 = gcc_designator i3 = initializer {node = self.autonode([d1, i3])} /* nfd jul 2005 */
   |  d2 = c99_designator i4 = initializer {node = self.autonode([d2, i4])}/* nfd jul 2005 */
   ;
 
/* Designated initializers, GCC extension, http://tigcc.ticalc.org/doc/gnuexts.html#SEC82 
 * These are of the form var = {structfield : val}
 * See below for c99 designators.
*/
gcc_designator returns[node]
	{wt = self.LT(1)}
	: id:ID COLON {node = self.node("designator", None, id.getText())}
	;

/* C99 designated initializers, http://david.tribble.com/text/cdiffs.htm#C90-vs-CPP98
 * Of the form var = { .structfield = val}
*/
c99_designator returns[node]
	{wt = self.LT(1)}
	: DOT id:ID EQUALS {node = self.node("designator", None, id.getText())}
	;

class_head returns [node]
	{wt = self.LT(1)}
	:	// Used only by predicates	
	{node = self.autonode(); node_leaves = []}
	("struct" {node_leaves.append("struct")}
	|"union" {node_leaves.append("union")}
	|"class" {node_leaves.append("class")}
	)
    (id:ID	{node_leaves.append(id.getText())}
		(LESSTHAN tal= template_argument_list {node.add_child(tal)} GREATERTHAN)?
		(bc = base_clause {node.add_child(bc)})? 
	)? LCURLY
	{node.set_leaf(node_leaves)}
	;

base_clause returns [node]
	{wt = self.LT(1)}
	:	COLON bs = base_specifier {bsl = bs} (COMMA bs = base_specifier {bsl.append(bs)})*
	{node = self.autonode(None, bsl)}
	;

base_specifier returns [bsl]
	:	// DW 13/08/03 Should check qualified_type for class-name?
	{bsl = []}
	(	"virtual" {bsl.append("virtual")} (as = access_specifier {bsl.append(as)})? qt = qualified_type {bsl.append(qt)}
	|	as = access_specifier {bsl.append(as)} ("virtual" {bsl.append("virtual")})? qt = qualified_type {bsl.append(qt)}
	|	qt = qualified_type {bsl.append(qt)}
	)
	;

access_specifier returns[as]
	:	"public" {as = "public"}
	|	"protected" {as = "protected"}
	|	"private" {as = "private"}
	;

member_declarator_list returns[node]
	{wt = self.LT(1)}
	:	md = member_declarator {node = self.autonode([md])}
		(COMMA md = member_declarator {node.add_child(md)} )*
	;

member_declarator returns[node]
	{wt = self.LT(1)}
	:	
		{node = self.node("member_declarator_bitfield")}
		((ID)? COLON constant_expression)=>(id:ID {node.set_leaf(id.getText())})? COLON c = constant_expression {node.add_child(c)} (ASSIGNEQUAL OCTALINT {node.add_attribute("purevirtual")})?
	|  
		d = declarator {node = self.autonode([d])} (ASSIGNEQUAL OCTALINT {node.set_leaf(0)})?
	;

conversion_function_decl_or_def returns[node]
	:	OPERATOR ds = declaration_specifiers (STAR | AMPERSAND)?	// DW 01/08/03 Use type_specifier here? see syntax
		(LESSTHAN tpl = template_parameter_list GREATERTHAN)?
		LPAREN (pl = parameter_list)? RPAREN	
		(tq = type_qualifier)?
		(es = exception_specification)?
		(	cs = compound_statement
		|	SEMICOLON! {self.end_of_stmt()}
		)
		{node = self.unfinishednode("conversion_function_decl_or_def")}
	;

// JEL note:  does not use (const|volatile)* to avoid lookahead problems
cv_qualifier_seq returns[cvs]
	:
	{cvs = ""} (tq = type_qualifier {cvs += tq})*
	;

declarator returns[node]
	{wt = self.LT(1)}
	:
		//{( !(LA(1)==SCOPE||LA(1)==ID) || self.qualifiedItemIsOneOf(qiPtrMember) )}?
		(ptr_operator)=> p = ptr_operator	// AMPERSAND or STAR
		d = declarator
		{node = self.autonode([d], p)}
	|	
		d = direct_declarator {node = d}
	;

direct_declarator returns[node]
	{wt = self.LT(1)}
	:
		(qualified_id LPAREN (RPAREN|declaration_specifiers) )=>	// Must be function declaration
		id = qualified_id {node = self.autonode(None, id)}
		{self.declaratorID(id, qiFun)}
		LPAREN {self.declaratorParameterList(0)}
		(pl = parameter_list {node.add_child(pl)} )?
		RPAREN {self.declaratorEndParameterList(0)}
		{tql = []}(tq = type_qualifier {tql.append(tq);node.add_child(tq)} )*
		(es = exception_specification {node.add_child(es)} )?
	|	(qualified_id LPAREN qualified_id)=>	// Must be class instantiation
		id = qualified_id
		{self.declaratorID(id,qiVar)}
		LPAREN
		el = expression_list
		RPAREN
		{node = self.autonode([el], id)}
	|
		(qualified_id LSQUARE)=>	// Must be array declaration
		id = qualified_id
{if self._td:
    self.declaratorID(id, qiType)
else:
    self.declaratorID(id, qiVar)
self.is_address = False
self.is_pointer = False;
}
		(options {warnWhenFollowAmbig = false;}:
		 {node = self.autonode([], id)}
                 LSQUARE (ce = constant_expression {node.add_child(ce)})?
                 RSQUARE)+
		{self.declaratorArray()}
		
	|
		id = qualified_id
{if self._td:
    self.declaratorID(id,qiType)
else:
    self.declaratorID(id,qiVar)
self.is_address = False
self.is_pointer = False;
}
		{node = self.autonode(None, id)}
	|	
		// DW 24/05/04 This block probably never entered as dtor selected out earlier
		//	Note In fact no dictionary entries for ctor or dtor	
		TILDE dtor:ID {declaratorID(dtor.getText(),qiDtor)}	// Note "class" not recorded in CPPSymbol
		{print "%d warning direct_declarator5 entered unexpectedly with %s" % (self.LT(1).getLine(),dtor.getText())}
		LPAREN {self.declaratorParameterList(0)}
		(pl = parameter_list)?
		RPAREN {self.declaratorEndParameterList(0)}
		{node = self.unfinishednode("direct-declarator")}
	|	
		LPAREN d = declarator RPAREN ds = declarator_suffixes
		{node = self.autonode([d, ds])}
	;

declarator_suffixes returns[node]
	:
	{node = self.autonode()}
	(
		(options {warnWhenFollowAmbig = false;}:
		 LSQUARE (cx = constant_expression {node.add_child(cx)})? RSQUARE)+
		{self.declaratorArray()}
	|	{(not ((self.LA(1)==LPAREN) and (self.LA(2)==ID)) or (self.qualifiedItemIsOneOf(qiType|qiCtor,1)))}?
		LPAREN {self.declaratorParameterList(0)}
		(pl = parameter_list {node.add_child(pl)})?
		RPAREN {self.declaratorEndParameterList(0)}
		(tq = type_qualifier {node.add_child(self.node(tq))} )*
		(es = exception_specification {node.add_child(es)} )?
//	|	// DW 28/06/04 deleted Assume either following bracketed declaration
//		// empty
	)
	;

/* I think something is weird with the context-guards for predicates;
 * as a result I manually hoist the appropriate pred from ptr_to_member
 *
 * TER: warning: seems that "ID::" will always bypass and go to 2nd alt :(
 */
function_declarator [definition] returns [node]
	{wt = self.LT(1)}
	:	
		//{( !(LA(1)==SCOPE||LA(1)==ID) || self.qualifiedItemIsOneOf(qiPtrMember) )}?
		(ptr_operator)=> po = ptr_operator fd = function_declarator[definition]
		{node = self.autonode([fd], po)}
	|	
		fdd = function_direct_declarator[definition]
		{node = self.autonode([fdd])}
	;

function_direct_declarator [definition]  returns [node]
	:
		/* predicate indicate that plain ID is ok here; this counteracts any
		 * other predicate that gets hoisted (along with this one) that
		 * indicates that an ID is a type or whatever.  E.g.,
		 * another rule testing isTypeName() alone, implies that the
		 * the ID *MUST* be a type name.  Combining isTypeName() and
		 * this predicate in an OR situation like this one:
		 * ( declaration_specifiers ... | function_declarator ... )
		 * would imply that ID can be a type name OR a plain ID.
		 */
		(	// fix prompted by (isdigit)() in xlocnum
			LPAREN
			q = qualified_id
			{self.declaratorID(q,qiFun)}
			RPAREN
		|
			(a = attribute)*
			q = qualified_id
			{self.declaratorID(q,qiFun)}
		)
		{node = self.autonode(None, q)}

{
if definition:
    self.myCode_function_direct_declarator(q)
}

		LPAREN 
{
self.functionParameterList()
if not self.K_and_R:
    self.in_parameter_list = True
}
		(pl = parameter_list {node.add_child(pl)} )? 
		{
if not self.K_and_R:
    self.in_parameter_list = False
else:
    self.in_parameter_list = True
		}
		RPAREN
		(an = func_asm_name {node.add_child(an)})?
		(options{warnWhenFollowAmbig = false;}:
		 tq = type_qualifier)*
		(ASSIGNEQUAL OCTALINT)?	// The value of the octal must be 0
		{self.functionEndParameterList(definition)}
		(es = exception_specification)?
	;

/* nfd: GCC extension, allows one to set the asm name of a function (the name
 * against which the function is linked).
 * Refer http://gcc.gnu.org/onlinedocs/gcc-3.3.1/gcc/Asm-Labels.html
*/
func_asm_name returns [node]
	:
	{node = self.autonode()}
	("asm" | "__asm" | "__asm__")
	LPAREN
	(sl:StringLiteral {node.extend_leaf(sl.getText())})+
	RPAREN
	;

ctor_definition returns [node]
	{wt = self.LT(1)}
	:
	ch = ctor_head
	cb = ctor_body
	{self.endConstructorDefinition()}
	{node = self.autonode([ch, cb])}
	;

ctor_head returns[node]
	{wt = self.LT(1)}
	:
	cds = ctor_decl_spec
	cd = ctor_declarator[1]
	{node = self.autonode([cds, cd])}
	;

ctor_decl_spec returns[node]
	{wt = self.LT(1)}
	:
	{node = self.autonode(); speclist = []}
	(("inline"|"_inline"|"__inline") {speclist.append("inline")} |"explicit" {speclist.append("explicit")})*
	{node.set_leaf(speclist)}
	;

ctor_declarator[definition] returns[node]
	: 
	// JEL 4/3/96 qualified_id too broad DW 10/06/03 ?
	q = qualified_ctor_id
	{self.declaratorParameterList(definition)}
	LPAREN (pl = parameter_list {q.add_child(pl)} )? RPAREN
	{self.declaratorEndParameterList(definition)}
	(es = exception_specification)?
	{node = self.unfinishednode("ctor-declarator")}
	;

// This matches a generic qualified identifier ::T::B::foo
// that is satisfactory for a ctor (no operator, no trailing <>)
qualified_ctor_id returns [q]
	{qitem = ""}
	: 
	so = scope_override
	{qitem = so}
	id:ID	// DW 24/05/04 Note. Neither Ctor or Dtor recorded in dictionary
	{qitem += id.getText(); q = qitem}
	;

ctor_body returns[node]
	{wt = self.LT(1)}
	:
	{node = self.autonode()}
	(ci = ctor_initializer {node.add_child(ci)} )?
	cs = compound_statement {node.add_child(ci)}
	;

ctor_initializer returns[node]
	:
	COLON superclass_init (COMMA superclass_init)*
	{node = self.unfinishednode("ctor-initialiser")}
	;

superclass_init
	{char *q;} 
	: 
	q = qualified_id LPAREN (el = expression_list)? RPAREN
	;

dtor_head[definition] returns[node]
	{wt = self.LT(1)}
	:
	dds = dtor_decl_spec
	dd = dtor_declarator[definition]
	{node = self.autonode([dd], dds)}
	;

dtor_decl_spec returns[sl]
	:
	{sl = []}
	(("inline"|"_inline"|"__inline"){sl.append("inline")}|"virtual"{sl.append("virtual")})*
	;

dtor_declarator[definition] returns[node]
	{wt = self.LT(1)}
	:	
	s = scope_override
	TILDE ID
	{self.declaratorParameterList(definition)}
	LPAREN RPAREN
	{self.declaratorEndParameterList(definition)}
	(es = exception_specification)?
	{node = self.autonode()}
	;

dtor_body returns[node]
	:
	cs = compound_statement {node = checknode(cs)}
	{self.endDestructorDefinition()}
	;

parameter_list returns [node]
	{wt = self.LT(1)}
	:	pdl = parameter_declaration_list {node = self.autonode([pdl])} (ELLIPSIS {node.set_leaf("...")} )?
	;

parameter_declaration_list returns[node]
	:	{node = self.autonode()}
	(	p = parameter_declaration {node.add_child(p)}
		(// Have not been able to find way of stopping warning of non-determinism between alt 1 and exit branch of block
		 COMMA p = parameter_declaration {node.add_child(p)}
		)*
	)
	;

parameter_declaration returns[node]
	:	{wt = self.LT(1); node = self.autonode(); self.beginParameterDeclaration()}
		(
			{not ((self.LA(1)==SCOPE) and (self.LA(2)==STAR or self.LA(2)==OPERATOR)) and ( not (self.LA(1)==SCOPE or self.LA(1)==ID) or self.qualifiedItemIsOneOf(qiType|qiCtor) )}?
			ds = declaration_specifiers	{node.add_child(ds)}// DW 24/3/98 Mods for K & R
			(  
				(declarator)=> d = declarator {node.add_child(d)}        // if arg name given
			| 
				ad = abstract_declarator {node.add_child(ad)}    // if arg name not given  // can be empty
			)
		|
			(declarator)=> d = declarator {node.add_child(d)}			// DW 24/3/98 Mods for K & R
		|
			ELLIPSIS {node.add_child(self.node("ellipsis"))}
		)
		(ASSIGNEQUAL 
		 re = remainder_expression {node.add_child(re)} // DW 18/4/01 assignment_expression
		)?
	;

type_name returns[node]// aka type_id
	{wt = self.LT(1)}
	:
	ds = declaration_specifiers ad = abstract_declarator
            {node = self.autonode([checknode(ds), checknode(ad)]);}
	;

/* This rule looks a bit weird because (...) can happen in two
 * places within the declaration such as "void (*)()" (ptr to
 * function returning nothing).  However, the () of a function
 * can only occur after having seen either a (abstract_declarator)
 * and not after a [..] or simple '*'.  These are the only two
 * valid () func-groups:
 *    int (*)();     // ptr to func
 *    int (*[])();   // array of ptr to func
 */
abstract_declarator returns[node]
	{wt = self.LT(1)}
	:	//{( !(LA(1)==SCOPE||LA(1)==ID) || self.qualifiedItemIsOneOf(qiPtrMember) )}?
		po = ptr_operator ad = abstract_declarator 
		{node = self.autonode([ad], po); node.set_result(po + ad.result)}
	|	
		LPAREN ad2 = abstract_declarator RPAREN
		{node = self.autonode([ad2])}
		(ads = abstract_declarator_suffix {node.add_child(ads)})+
		{node.set_result(None)}
	|	
		{node = self.autonode(None, "arrayindex")}
		(LSQUARE (cx = constant_expression {node.add_child(cx)})? RSQUARE {self.declaratorArray()}
		)+
		{node.set_result(None)}
	|	
		{node = self.autonode()}
		{node.set_result("")}
		/* empty */
	;

abstract_declarator_suffix returns[node]
	{wt = self.LT(1)}
	:	
		{node = self.autonode(None, "arrayindex")}
		LSQUARE (cx = constant_expression {node.add_child(cx)} )? RSQUARE
		{self.declaratorArray()}
	|
		LPAREN
		{node = self.autonode()}
		{self.declaratorParameterList(0);}
		(pl = parameter_list {node.add_child(pl)} )?
		RPAREN
		cvs = cv_qualifier_seq {node.set_leaf(["()", cvs])}
		{self.declaratorEndParameterList(0)}
		(es = exception_specification {node.add_child(es)} )?
	;

exception_specification returns[node]
	:	"throw" 
		LPAREN 
		(	(so = scope_override ID (COMMA so = scope_override ID)? )? 
		|	ELLIPSIS
		)
		RPAREN
		{node = self.unfinishednode("exception-spec")}
	;

template_head returns [node]
	{wt = self.LT(1)}
	:	
		"template"
		LESSTHAN tpl = template_parameter_list GREATERTHAN
		{node = self.autonode([tpl])}
	;

template_parameter_list returns [node]
	{wt = self.LT(1)}
	:	
		{self.beginTemplateParameterList()}
		{node = self.autonode()}
		tp = template_parameter {node.add_child(tp)} (COMMA tp = template_parameter {node.add_child(tp)} )*
		{self.endTemplateParameterList()}
	;

/* Rule requires >2 lookahead tokens. The ambiguity is resolved 
 * correctly, however. According to the manual "...A template argument
 * that can be interpreted either as a parameter-declaration or a
 * type-argument (because its identifier is the name of an
 * already existing class) is taken as type-argument."
 * Therefore, any "class ID" that is seen on the input, should
 * match the first alternative here (it should be a type-argument).
 */
template_parameter returns[node]
	:
	(options{generateAmbigWarnings = false;}:
		("class"|"typename") 
		(id:ID  (ASSIGNEQUAL assigned_type_name)? )?
		{self.templateTypeParameter(id.getText())}
	|	
		p = parameter_declaration	// DW 30/06/03 This doesn't seem to match the current standard
	)
	{node = self.unfinishednode("template-parameter")}
	;

/* This is to allow an assigned type_name in a template parameter
 *	list to be defined previously in the same parameter list,
 *	as type setting is ineffective whilst guessing
 */
assigned_type_name
	:
	(options{generateAmbigWarnings = false;}:
		s = qualified_type ad = abstract_declarator	
	|
		ts = simple_type_specifier ad = abstract_declarator
	)
	;

// This rule refers to an instance of a template class or function
template_id	// aka template_class_name
	:	ID LESSTHAN tal = template_argument_list GREATERTHAN
	;

template_argument_list returns [node]
	{wt = self.LT(1)}
	:	ta = template_argument {node = self.autonode(ta)} (COMMA ta = template_argument {node.add_child(ta)} )*
	;

/* Here assignment_expression was changed to shift_expression to rule out
 *  x< 1<2 > which causes ambiguities. As a result, these can be used only
 *  by enclosing parentheses x<(1<2)>. This is true for x<1+2> ==> bad,
 *  x<(1+2)> ==> ok.
 */
template_argument returns[node]
	:
		{( not (self.LA(1)==SCOPE or self.LA(1)==ID) or self.qualifiedItemIsOneOf(qiType|qiCtor) )}?
		tn = type_name
	|	sx = shift_expression // failed in iosfwd
//	|	assignment_expression	// Inserted as per grammar summary
	{node = self.unfinishednode("template-arg")}
	;

///////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////
//////////////////////////////  STATEMENTS ////////////////////////////
///////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////

statement_list returns [node]
	{accum = []}
	:	({wt = self.LT(1)} s = statement {accum.append(s)} {wt = self.LT(1)} )+ {node = self.autonode(accum)}
	;

statement returns [node]
	{wt = self.LT(1)}
	:
	(	(declaration)=> d = declaration {node = self.autonode([d])}
	|	labeled_statement {node = self.unfinishednode("statement_labeled")}
	|	s = case_statement {node = s}
	|	ds = default_statement {node = ds}
	|	e = expression  {node = checknode(e)} SEMICOLON! {self.end_of_stmt()}
	|	compstat = compound_statement {node = checknode(compstat)}
	|	sstat = selection_statement {node = checknode(sstat)}
	|	iterstat = iteration_statement {node = checknode(iterstat)}
	|	jumpstat = jump_statement {node = checknode(jumpstat)}
	|	SEMICOLON! {self.end_of_stmt()} {node = self.blanknode()}
	|	try_block {node = self.unfinishednode("statement_try")}
	|	throw_statement {node = self.unfinishednode("statement_throw")}
	|	asm = asm_block {node = asm}
	)
	;

labeled_statement
	:	ID COLON s = statement
	;

case_statement returns [node]
	:	"case"
		cx = constant_expression COLON s = statement {node = self.autonode([cx, s])}
	;

default_statement returns [node]
	:	"default" {node = self.autonode()} COLON s = statement {node.add_child(s)}
	;

compound_statement returns [node]
	:	LCURLY 
{
self.end_of_stmt()
self.enterNewLocalScope()
}
	{wt = self.LT(1)}
		{node = self.autonode()}
		(sl = statement_list {node.add_child(sl)} )?
		RCURLY {self.exitLocalScope()}
	;

/* NOTE: cannot remove ELSE ambiguity, but it parses correctly.
 * The warning is removed with the options statement
 */
selection_statement returns [node]
	{wt = self.LT(1)}
	:	
		"if"^ LPAREN 
		x1 = expression RPAREN
		s1 = statement
		{node = self.autonode([x1, s1], "if")}
		(options {warnWhenFollowAmbig = false;}:
		 "else"^ s2 = statement {elsenode = self.node("else", [s2]); node.add_child(elsenode)})?
	|	
		"switch"^ LPAREN  x3 = expression RPAREN s3 = statement
		{node = self.autonode([x3, s3], "switch")}
	;

iteration_statement returns [node]
	{wt = self.LT(1)}
	:	
		"while"	
		LPAREN x1 = expression RPAREN 
		s1 = statement  
		{node = self.autonode([x1, s1], "while")}
	|	
		"do" 
		s2 = statement "while"
		LPAREN x2 = expression RPAREN 
		SEMICOLON! {self.end_of_stmt()} 
		{node = self.autonode([s2, x2], "do")}
	|	
		"for" LPAREN {node = self.autonode(); node.set_leaf("for")}
		(	(declaration)=> d1 = declaration {node.add_child(d1)}
		|	x3 = expression SEMICOLON! {self.end_of_stmt()} {node.add_child(x3)}
		|	SEMICOLON! {self.end_of_stmt()}  {node.add_child(self.node("blank"))}
		)
		{x4 = self.blanknode()}
		{x5 = self.blanknode()}
		(x4 = expression)? SEMICOLON! {self.end_of_stmt()}
		(x5 = expression)?
		{node.add_child(x4)} {node.add_child(x5)}
		RPAREN s3 = statement	  {node.add_child(s3)}
	;

jump_statement returns[node]
	{wt = self.LT(1)}
	:	
	(	"goto" gotoid:ID SEMICOLON! {self.end_of_stmt()} {node = self.node("jump_goto", None, gotoid)}
	|	"continue" SEMICOLON! {self.end_of_stmt()} {node = self.autonode(None, "continue")}
	|	"break" SEMICOLON! {self.end_of_stmt()} {node = self.autonode(None, "break")}
		// DW 16/05/03 May be problem here if return is followed by a cast expression 
	|	"return" {self.in_return = True}
//		(	options{warnWhenFollowAmbig = false;}:
//			(LPAREN {(self.qualifiedItemIsOneOf(qiType) )}? ID RPAREN)=> 
//			LPAREN ID RPAREN (expression)?	// This is an unsatisfactory fix for problem in xstring re "return (allocator);"
//											//  and in xlocale re return (_E)(_Tolower((unsigned char)_C, &_Ctype));
//			//{printf("%d CPP_parser.g jump_statement Return fix used\n",self.LT(1).getLine());}
//		|	(expression  
//		/* nfd: fixme: I commented out the above weirdo thing */
//		)?	SEMICOLON!
            {node = self.autonode([], "return")}
	   (x = expression {node.add_child(x)} )? SEMICOLON!
{
self.in_return = False
self.end_of_stmt() 
}
	)
	;

try_block
	:	"try" compstat = compound_statement (handler)*
	;


handler
	:	"catch"
		{self.exceptionBeginHandler()}
		{self.declaratorParameterList(1)}
		LPAREN exception_declaration RPAREN
		{self.declaratorEndParameterList(1)}
		compstat = compound_statement
		{self.exceptionEndHandler()}
	;

exception_declaration
	:	pdl = parameter_declaration_list
	;

/* This is an expression of type void according to the ARM, which
 * to me means "statement"; it removes some ambiguity to put it in
 * as a statement also.
 */
throw_statement
	:	"throw" (ax = assignment_expression) ? SEMICOLON! { self.end_of_stmt();}
	;

using_declaration returns [node]
	:	"using"
		("namespace" qid = qualified_id	// Using-directive
		|qid = qualified_id				// Using-declaration
		)
		SEMICOLON! {self.end_of_stmt();}
		{node = self.unfinishednode("using-decl")}
	;

asm_block returns[node]
	{node = self.autonode()}
	:   std_asm_block | gcc_asm_block
	;

std_asm_block
	:	("_asm"|"__asm") LCURLY (~RCURLY)* RCURLY 
	;

gcc_asm_block
	:   ("__asm__" | "asm") ("volatile" | "__volatile__" | "__volatile")?
            LPAREN asm_code
            ((COLON | SCOPE) (asm_reg_spec)?)*
            RPAREN
	;

asm_code
	: (StringLiteral)+
	;

asm_reg_spec
	:
        (LSQUARE ID RSQUARE)?
        StringLiteral
        (LPAREN ex1 = expression RPAREN)?
        (COMMA (LSQUARE ID RSQUARE)? StringLiteral
        (LPAREN ex2 = expression RPAREN)? )*
	;

///////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////
//////////////////////////////  EXPRESSIONS ///////////////////////////
///////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////

expression returns [node]
	{wt = self.LT(1)}
	:	x = assignment_expression {node = x} (COMMA x = assignment_expression {node = self.exprnode_acc(node, x, "comma");})*
	;

/* right-to-left for assignment op */
assignment_expression returns[node]
	{wt = self.LT(1)}
	:	cx = conditional_expression {node = checknode(cx)}
		(	(ASSIGNEQUAL {leaf="equals"} |TIMESEQUAL {leaf="timesequals"}|DIVIDEEQUAL {leaf="dividesequals"}
			|MINUSEQUAL{leaf="minusequals"}|PLUSEQUAL {leaf="plusequals"}
			|MODEQUAL{leaf="modequals"}
			|SHIFTLEFTEQUAL{leaf="shiftleftequals"}
			|SHIFTRIGHTEQUAL{leaf="shiftrightequals"}
			|BITWISEANDEQUAL{leaf="bitwiseandequals"}
			|BITWISEXOREQUAL{leaf="bitwisexorequals"}
			|BITWISEOREQUAL{leaf="bitwiseorequals"}
			)
			rx = remainder_expression {node = self.exprnode_acc(node, checknode(rx), leaf)}
		)?
	;

remainder_expression returns[node]
	:
		(	(conditional_expression (COMMA|SEMICOLON|RPAREN)
			)=>
			{self.assign_stmt_RHS_found += 1}
			x = assignment_expression {node = checknode(x)}
{
if self.assign_stmt_RHS_found > 0:
    self.assign_stmt_RHS_found -= 1
else:
    print "%d warning Error in assign_stmt_RHS_found = %d" % (self.LT(1).getLine(), self.assign_stmt_RHS_found);
}
		|	
			x = assignment_expression {node = checknode(x)}
		)
	;

conditional_expression returns[node]
	{wt = self.LT(1)}
	:	
		lx = logical_or_expression {node = checknode(lx)}
		(QUESTIONMARK e = expression COLON cx = conditional_expression
		{node = self.node("expression"); node.add_child(lx); node.add_child(e); node.add_child(cx); node.set_leaf("c_ternary")})?
	;

constant_expression returns[node]
	:	
		cs = conditional_expression
		{node = checknode(cs)}
	;

logical_or_expression returns[node]
	{wt = self.LT(1)}
	:	
		x = logical_and_expression {node = x} (OR x = logical_and_expression {node = self.exprnode_acc(node, x, "logicalor")})* 
	;

logical_and_expression returns[node]
	{wt = self.LT(1)}
	:	
		x = inclusive_or_expression {node = x} (AND x = inclusive_or_expression {node = self.exprnode_acc(node, x, "logicaland")})* 
	;

inclusive_or_expression returns[node]
	{wt = self.LT(1)}
	:	
		x = exclusive_or_expression {node = x} (BITWISEOR x = exclusive_or_expression {node = self.exprnode_acc(node, x, "bitwiseor")})*
	;

exclusive_or_expression returns[node]
	{wt = self.LT(1)}
	:	
		x = and_expression {node = x} (BITWISEXOR x = and_expression {node = self.exprnode_acc(node, x, "bitwisexor")})*
	;

and_expression returns[node]
	{wt = self.LT(1)}
	:	
	x = equality_expression {node = x} (AMPERSAND x = equality_expression {node = self.exprnode_acc(node, x, "bitwiseand")})*
	;

equality_expression returns[node]
	{wt = self.LT(1)}
	:	
		x = relational_expression {node = x} ((NOTEQUAL {leaf = "notequals"}| EQUAL {leaf = "equals"}) x = relational_expression {node = self.exprnode_acc(node, x, leaf)})*
	;

relational_expression returns[node]
	{wt = self.LT(1)}
	:	x = shift_expression {node = x}
		(options {warnWhenFollowAmbig = false;}:
			(	LESSTHAN {leaf = "lessthan"}
			|	GREATERTHAN {leaf = "greaterthan"}
			|	LESSTHANOREQUALTO {leaf = "lessthanorequals"}
			|	GREATERTHANOREQUALTO {leaf = "greaterthanorequals"}
			)
		 x = shift_expression {node = self.exprnode_acc(node, x, leaf)}
		)*
	;

shift_expression returns[node]
	{wt = self.LT(1)}
	:	x = additive_expression {node = x} ((SHIFTLEFT {leaf = "shiftleft"}| SHIFTRIGHT {leaf = "shiftright"}) x = additive_expression {node = self.exprnode_acc(node, x, leaf)})*
	;

/* See comment for multiplicative_expression regarding #pragma */
additive_expression returns[node]
	{wt = self.LT(1)}
	:	x = multiplicative_expression {node = x}
		(options{warnWhenFollowAmbig = false;}:
			(PLUS {leaf = "add"}| MINUS {leaf = "subtract"}) x = multiplicative_expression
			{node = self.exprnode_acc(node, x, leaf)}
		)*
	;

/* ANTLR has trouble dealing with the analysis of the confusing unary/binary
 * operators such as STAR, AMPERSAND, PLUS, etc...  With the #pragma (now "(options{warnWhenFollowAmbig = false;}:" etc.)
 * we simply tell ANTLR to use the "quick-to-analyze" approximate lookahead
 * as full LL(k) lookahead will not resolve the ambiguity anyway.  Might
 * as well not bother.  This has the side-benefit that ANTLR doesn't go
 * off to lunch here (take infinite time to read grammar).
 */
multiplicative_expression returns[node]
	{wt = self.LT(1)}
	:	x = pm_expression {node = x}
		(options{warnWhenFollowAmbig = false;}:
			(STAR {leaf = "multiply"}| DIVIDE {leaf = "divide"}| MOD {leaf = "modulus"}) x = pm_expression
			{node = self.exprnode_acc(node, x, leaf)}
		)*
	;

/* Pointer to member operators*/
pm_expression returns[node]
	{wt = self.LT(1)}
	:	x = cast_expression {node = x} ((DOTMBR {leaf = ".*"}| POINTERTOMBR {leaf = "->*"}) x = cast_expression {node = self.exprnode_acc(node, x, leaf)})*
	;

/* The string "( ID" can be either the start of a cast or
 * the start of a unary_expression.  However, the ID must
 * be a type name for it to be a cast.  Since ANTLR can only hoist
 * semantic predicates that are visible without consuming a token,
 * the semantic predicate in rule type_name is not hoisted--hence, the
 * rule is reported to be ambiguous.  I am manually putting in the
 * correctly hoisted predicate.
 *
 * Ack! Actually "( ID" might be the start of "(T(expr))" which makes
 * the first parens just an ordinary expression grouping.  The solution
 * is to look at what follows the type, T.  Note, this could be a
 * qualified type.  Yucko.  I believe that "(T(" can only imply
 * function-style type cast in an expression (...) grouping.
 *
 * We DO NOT handle the following situation correctly at the moment:
 * Suppose you have
 *    struct rusage rusage;
 *    return (rusage.fp);
 *    return (rusage*)p;
 * Now essentially there is an ambiguity here. If rusage is followed by any
 * postix operators then it is an identifier else it is a type name. This
 * problem does not occur in C because, unless the tag struct is attached,
 * rusage is not a type name. However in C++ that restriction is removed.
 * No *real* programmer would do this, but it's in the C++ standard just for
 * fun..
 *
 * Another fun one (from an LL standpoint):
 *
 *   (A::B::T *)v;      // that's a cast of v to type A::B::T
 *   (A::B::foo);    // that's a simple member access
 *
 * The qualifiedItemIs(1) function scans ahead to what follows the
 * final "::" and returns qiType if the item is a type.  The offset of
 * '1' makes it ignore the initial LPAREN; normally, the offset is 0.
 */

cast_expression returns[node]
	{wt = self.LT(1)}
	:
	(compound_literal) => x = compound_literal {node = x} /* nfd */
	|
		// DW 23/06/03
		(LPAREN (type_qualifier)? type_specifier[dsInvalid] (ptr_operator)* RPAREN)=>
		 {node = self.exprnode(None, leaf = "cast")}
		 LPAREN (tq = type_qualifier {node.add_child(self.node(tq))})? ts = type_specifier[dsInvalid]
		 {indirection = self.node("indirection", leaf = "")}
		 (po = ptr_operator {indirection.leaf += "*"})* RPAREN cx = cast_expression {node.add_children([ts, indirection, cx])}
	|  
		x = unary_expression {node = x}	// handles outer (...) of "(T(expr))"
	;

unary_expression returns[node]
	{wt = self.LT(1)}
	:
		(	//{!(LA(1)==TILDE && LA(2)==ID)||self.qualifiedItemIsOneOf(qiVar|qiFun|qiDtor|qiCtor)}?
			(postfix_expression)=> x = postfix_expression {node = x}
		|	PLUSPLUS x = unary_expression {node = self.exprnode([x], "++.")}
		|	MINUSMINUS x= unary_expression {node = self.exprnode([x], "--.")}
		|	op = unary_operator x = cast_expression {node = self.exprnode([x], op); node.set_result(None)}
		|	"sizeof" {node = self.exprnode(leaf = "sizeof")}
			(// see comment for rule cast_expression for info on predicate
			 // JEL NOTE 3/31/96 -- This won't work -- you really need to
			 // call self.qualifiedItemIsOneOf(qiType|qiCtor,1)
			 // The context should also be ( LPAREN (SCOPE|ID) )
			 //	( LPAREN ID ) => {self.isTypeName((LT(2)->getText()).data())}?
			 {(not (((self.LA(1)==LPAREN and (self.LA(2)==ID)))) or (self.isTypeName(self.LT(2).getText())))}?
				LPAREN x = type_name RPAREN {node.add_child(x)}
			|	x = unary_expression {node.add_child(x)}
			)
		|   {node = self.exprnode()}
			(s:SCOPE {node.set_leaf(s)})?
			(x = new_expression {node.add_child(x)}
			|x = delete_expression {node.add_child(x)}
			)
		)
	;

postfix_expression returns[node]
	{wt = self.LT(1)}
	:
	(	
		options {warnWhenFollowAmbig = false;}:
		// Function-style cast must have a leading type
		{(self.LA(1) != LPAREN)}?
		(ts = simple_type_specifier LPAREN RPAREN LPAREN)=>	// DW 01/08/03 To cope with problem in xtree (see test10.i)
		 ts = simple_type_specifier LPAREN RPAREN LPAREN (el = expression_list)? RPAREN
		{node = self.unfinishednode("postfix-expr")}
	|
		{(self.LA(1) != LPAREN)}?
		(ts = simple_type_specifier LPAREN)=>
		 ts = simple_type_specifier LPAREN (el = expression_list)? RPAREN
		{node = self.unfinishednode("postfix-expr-2")}
	|  
		x = primary_expression {node = x}
		(options {warnWhenFollowAmbig = false;}:
        	LSQUARE x = expression RSQUARE {node = self.exprnode([node, x], "arrayindex")}
		|	LPAREN {node = self.exprnode([node], "call")}
			(x2 = expression_list {node.add_child(x2)} )?
			RPAREN
		|	DOT idxpr = id_expression
				{node = self.exprnode([node, idxpr], "postfixdot")}
		|	POINTERTO idxpr2 = id_expression
				{node = self.exprnode([node, idxpr2], "postfixptr")}
		|	PLUSPLUS {node = self.exprnode([node], "plusplus")}
		|	MINUSMINUS {node = self.exprnode([node], "minusminus")}
		)*
	|
		("dynamic_cast"|"static_cast"|"reinterpret_cast"|"const_cast")	// Note const_cast in elsewhere
		LESSTHAN ts = type_specifier[ds] (po = ptr_operator)? GREATERTHAN
		LPAREN x = expression RPAREN
		{node = self.unfinishednode("postfix-expr-3")}
	)
	;

primary_expression returns[node]
	{wt = self.LT(1)}
	:	x = id_expression {node = x}
	|	x = constant {node = x}
	|	"this" {node = self.exprnode(None, "this", result = "this")}
	|	LPAREN x = expression RPAREN {node = x}
	;

compound_literal returns[node]
	{wt = self.LT(1)}
	/* nfd: ISO C99 "compound literals" feature (extension) */
	: LPAREN tn = type_name RPAREN LCURLY i = initializer RCURLY
	{node = self.autonode([tn, i])}
	;

id_expression returns[node]
	{wt = self.LT(1); node = self.exprnode([], "id_expression")}
	:
		out = scope_override
		(	id1:ID {out += id1.getText()}
		|	OPERATOR o = optor {out += "UNFINISHED operator " + o}
		|	TILDE {out += "UNFINISHED ~"} (STAR {out += "UNFINISHED *"} )? id2:ID {out += "UNFINISHED" + id2.getText()}	// DW 29/07/03 STAR included to allow for *_S = ~*_S; seen in vector
		)
		{node.add_attribute("value", out); node.result = out}
	;

unary_operator returns[op]
	:	AMPERSAND {op = "unary_ampersand"}
	|	STAR {op = "unary_pointsto"}
	|	PLUS {op = "unary_plus"}
	|	MINUS {op = "unary_minus"}
	|	TILDE {op = "unary_tilde"}
	|	NOT {op = "unary_not"}
	;

/* JEL The first ()? is used to resolve "new (expr) (type)" because both
 * (expr) and (type) look identical until you've seen the whole thing.
 *
 * new_initializer appears to be conflicting with function arguments as
 * function arguments can follow a primary_expression.  [This is a full
 * LL(k) versus LALL(k) problem.  Enhancing context by duplication of
 * some rules might handle this.]
 */
new_expression returns[node]
	:
	(  
		"new"
		((LPAREN expression_list RPAREN)=> 
			LPAREN el = expression_list RPAREN)?
		(new_type_id | LPAREN tn = type_name RPAREN)
		(options{warnWhenFollowAmbig = false;}:	
		(new_initializer)=> new_initializer)?
	)
	{node = self.unfinishednode("new-expr")}
	;

new_initializer
	:	LPAREN (el = expression_list)? RPAREN
	;

new_type_id
	:	ds = declaration_specifiers 
		(options {warnWhenFollowAmbig = false;}:
		 //{( !(LA(1)==SCOPE||LA(1)==ID) || self.qualifiedItemIsOneOf(qiPtrMember) )}?
			new_declarator 
		)?
	;

new_declarator
	:	//{( !(LA(1)==SCOPE||LA(1)==ID) || self.qualifiedItemIsOneOf(qiPtrMember) )}?  
		//ptr_to_member cv_qualifier_seq 
		po = ptr_operator
		(options {warnWhenFollowAmbig = false;}:
		new_declarator ) ?
	|	direct_new_declarator
	;

ptr_operator returns[oper]
	:	{oper="UNKNOWN"} (	AMPERSAND {oper = "&"}	{self.is_address = True}
		|	("_cdecl"|"__cdecl") {oper = "_cdecl"}
		|	("_near"|"__near") {oper = "_near"}
		|	("_far"|"__far") {oper = "_far"}
		|	"__interrupt" {oper = "__interrupt"}
		|	("pascal"|"_pascal"|"__pascal") {oper = "pascal"}
		|	("_stdcall"|"__stdcall") {oper = "_stdcall"}
		|	ptm = ptr_to_member	{oper = ptm}// e.g. STAR 
		)	
   ;

// Match A::B::*
ptr_to_member returns[ptm]
	:
	s = scope_override {ptm = s} STAR {ptm += "*"}  {self.is_pointer = True} cvs = cv_qualifier_seq {ptm += cvs}
	;

// Match the A::B::C:: or nothing
scope_override returns [s]
	{sitem = ""}
	:
		//{!(self.qualifiedItemIsOneOf(qiType))}?
		(SCOPE {sitem += "::"} )?
		(	options {warnWhenFollowAmbig = false;}:
			{self.scopedItem()}?
			id:ID (LESSTHAN tal = template_argument_list GREATERTHAN)? SCOPE
{
//printf("scope_override entered\n");
sitem += id.getText() + "::"
}
		)*
	{s = sitem;
	}
	;

/* The "[expression]" construct conflicts with the "new []" construct
 * (and possibly others).  We used approximate lookahead for the "new []"
 * construct so that it would not try to compute full LL(2) lookahead.
 * Here, we use #pragma approx again because anytime we see a [ followed
 * by token that can begin an expression, we always want to loop.
 * Approximate lookahead handles this correctly.  In fact, approximate
 * lookahead is the same as full lookahead when all but the last lookahead
 * depth are singleton sets; e.g., {"["} followed by FIRST(expression).
 */
direct_new_declarator
	:
		(options {warnWhenFollowAmbig = false;}:
			LSQUARE x = expression RSQUARE
		)+
	;

delete_expression returns[node]
	{wt = self.LT(1)}
	:	"delete" {node = self.node("delete")} (LSQUARE RSQUARE {node.set_leaf("arrayindex")} )? cx = cast_expression {node.add_child(cx)}
	;

expression_list returns[node]
	{wt = self.LT(1)}
	:	x = assignment_expression {node = self.node("expression", [x], leaf = "comma")}(COMMA x = assignment_expression {node.add_child(x)})*
	;

constant returns[node]
	{wt = self.LT(1)}
	:	{node = self.node("expression", leaf = "literal"); v = ""}
	(	oi:OCTALINT {v = oi.getText(); t = "int"}
	|	di:DECIMALINT {v = di.getText(); t = "int"}
	|	hi:HEXADECIMALINT {v = hi.getText(); t = "int"}
	|	cl:CharLiteral {v = cl.getText(); t = "char"}
	|	(sl:StringLiteral {v += sl.getText()})+ {t = "string"}
	|	f1:FLOATONE {v = f1.getText(); t = "float"}
	|	f2:FLOATTWO {v = f2.getText(); t = "float"}
	|	"true" {v = "true"; t = "bool"}
	|	"false" {v = "false"; t = "bool"}
	)
{
litnode = self.node(t); litnode.set_leaf(v)
node.add_child(litnode)
}
	;

optor  returns[out]
	:
		"new" {out = "new"}
		(options {warnWhenFollowAmbig = false;}:
			LSQUARE RSQUARE | )		// check syntax
	|   
		"delete" {out = "delete"}
		(options {warnWhenFollowAmbig = false;}:
			LSQUARE RSQUARE | )		// check syntax
	|	LPAREN RPAREN {out = "()"}
	|	LSQUARE RSQUARE {out = "arrayindex"}
	|	x = optor_simple_tokclass {out = x}	//OPTOR_SIMPLE_TOKCLASS
	;

//Zuo 5/11/2001
// This is the equivalent to "#tokclass OPTOR_SIMPLE_TOKCLASS" in cplusplus.g

optor_simple_tokclass returns[out]
	:
    (PLUS|MINUS|STAR|DIVIDE|MOD|BITWISEXOR|AMPERSAND|BITWISEOR|TILDE|NOT|
	 SHIFTLEFT|SHIFTRIGHT|
	 ASSIGNEQUAL|TIMESEQUAL|DIVIDEEQUAL|MODEQUAL|PLUSEQUAL|MINUSEQUAL|
	 SHIFTLEFTEQUAL|SHIFTRIGHTEQUAL|BITWISEANDEQUAL|BITWISEXOREQUAL|BITWISEOREQUAL|
	 EQUAL|NOTEQUAL|LESSTHAN|GREATERTHAN|LESSTHANOREQUALTO|GREATERTHANOREQUALTO|OR|AND|
	 PLUSPLUS|MINUSMINUS|COMMA|POINTERTO|POINTERTOMBR
	)
	;

// Zuo 19/11/01 from next line, the Lexer is derived from stdCParser.g

class CPPLexer extends Lexer;

options
	{
	k = 3;
	exportVocab = STDC;
	testLiterals = true;
	}

// DW 4/11/02 put in to support manual hoisting
tokens
	{
	OPERATOR = "operator";
	}

{
def setOriginalSource(src):
    originalSource = src
    lineObject.setSource(src)

def setSource(src):
    lineObject.setSource(src)
  

}

/* Operators: */

ASSIGNEQUAL     : '=' ;
COLON           : ':' ;
COMMA           : ',' ;
QUESTIONMARK    : '?' ;
SEMICOLON       : ';' ;
POINTERTO       : "->" ;

/*
// DOT & ELLIPSIS are commented out since they are generated as part of
// the Number rule below due to some bizarre lexical ambiguity shme.
// DOT  :       '.' ;
// ELLIPSIS      : "..." ;
*/

LPAREN          : '(' ;
RPAREN          : ')' ;
LSQUARE         : '[' ;
RSQUARE         : ']' ;
LCURLY          : '{' ;
RCURLY          : '}' ;

EQUAL           : "==" ;
NOTEQUAL        : "!=" ;
LESSTHANOREQUALTO     : "<=" ;
LESSTHAN              : "<" ;
GREATERTHANOREQUALTO  : ">=" ;
GREATERTHAN           : ">" ;

DIVIDE          : '/' ;
DIVIDEEQUAL     : "/=" ;
PLUS            : '+' ;
PLUSEQUAL       : "+=" ;
PLUSPLUS        : "++" ;
MINUS           : '-' ;
MINUSEQUAL      : "-=" ;
MINUSMINUS      : "--" ;
STAR            : '*' ;
TIMESEQUAL      : "*=" ;
MOD             : '%' ;
MODEQUAL        : "%=" ;
SHIFTRIGHT      : ">>" ;
SHIFTRIGHTEQUAL : ">>=" ;
SHIFTLEFT       : "<<" ;
SHIFTLEFTEQUAL  : "<<=" ;

AND            : "&&" ;
NOT            : '!' ;
OR             : "||" ;

AMPERSAND       : '&' ;
BITWISEANDEQUAL : "&=" ;
TILDE           : '~' ;
BITWISEOR       : '|' ;
BITWISEOREQUAL  : "|=" ;
BITWISEXOR      : '^' ;
BITWISEXOREQUAL : "^=" ;

//Zuo: the following tokens are come from cplusplus.g

POINTERTOMBR    : "->*" ;
DOTMBR          : ".*"  ;

SCOPE           : "::"  ;

// DW 10/10/02
// Whitespace -- ignored
Whitespace	
	:	(	(' ' |'\t' | '\f')
			// handle newlines
		|	(	"\r\n"  // MS
			|	'\r'    // Mac
			|	'\n'    // Unix 
			)	{ $nl; }
			// handle continuation lines
		|	(	"\\\r\n"  // MS
			|	"\\\r"    // Mac
			|	"\\\n"    // Unix 
			)	{ $nl; }
		)	
		{$skip}
	;

Comment  
	:	"/*"   
		(	{self.LA(2) != '/'}? '*'
		|	EndOfLine { $nl;}
		|	~('*'| '\r' | '\n')
		)*
		"*/" {$skip}
	;

CPPComment
	:	"//" (~('\n' | '\r'))* EndOfLine
 		{$skip; $nl;}                     
	;

PREPROC_DIRECTIVE
	options{paraphrase = "a line directive";}
	:	'#' LineDirective
		{$skip; $nl;} 
	;

protected
ALL_TO_NL
	: (~(' ' | '\t' | '\f')) (~('\n' | '\r'))*
	;

protected 
LineDirective
	:
		("line")?  // this would be for if the directive started "#line"
		(Space)+
		n:Decimal
		(Space)+
		(sl:ALL_TO_NL)
		{
self.line_directive_handler(sl.getText(), n.getText())  // see main()
		}
		EndOfLine
	;

protected  
Space
	:	(' ' | '\t' | '\f')
	;


Pragma
	:	('#' "pragma" (~('\r' | '\n'))* EndOfLine)
		{$skip; $nl;}
	;

Error
	:	('#' "error" (~('\r' | '\n'))* EndOfLine)
		{$skip; $nl;}
	;

/* Literals: */

/*
 * Note that we do NOT handle tri-graphs nor multi-byte sequences.
 */

/*
 * Note that we can't have empty character constants (even though we
 * can have empty strings :-).
 */
CharLiteral
	:	'\'' (Escape | ~( '\'' )) '\''
	;

/*
 * Can't have raw imbedded newlines in string constants.  Strict reading of
 * the standard gives odd dichotomy between newlines & carriage returns.
 * Go figure.
 */
StringLiteral
	:	'"'
		( Escape
		|	(	"\\\r\n"   // MS 
			|	"\\\r"     // MAC
			|	"\\\n"     // Unix
			)	{ $nl; }
		|	~('"' | '\r' | '\n' | '\\')
		)*
		'"'
	;

protected
EndOfLine
	:	(	options{generateAmbigWarnings = false;}:
			"\r\n"  // MS
		|	'\r'    // Mac
		|	'\n'    // Unix
		)
	;

/*
 * Handle the various escape sequences.
 *
 * Note carefully that these numeric escape *sequences* are *not* of the
 * same form as the C language numeric *constants*.
 *
 * There is no such thing as a binary numeric escape sequence.
 *
 * Octal escape sequences are either 1, 2, or 3 octal digits exactly.
 *
 * There is no such thing as a decimal escape sequence.
 *
 * Hexadecimal escape sequences are begun with a leading \x and continue
 * until a non-hexadecimal character is found.
 *
 * No real handling of tri-graph sequences, yet.
 */

protected
Escape  
	:	'\\'
		( options{warnWhenFollowAmbig=false;}:
		  'a'
		| 'b'
		| 'f'
		| 'n'
		| 'r'
		| 't'
		| 'v'
		| '"'
		| '\''
		| '\\'
		| '?'
		| ('0'..'3') (options{warnWhenFollowAmbig=false;}: Digit (options{warnWhenFollowAmbig=false;}: Digit)? )?
		| ('4'..'7') (options{warnWhenFollowAmbig=false;}: Digit)?
		| 'x' (options{warnWhenFollowAmbig=false;}: Digit | 'a'..'f' | 'A'..'F')+
		)
	;

/* Numeric Constants: */

protected
Digit
	:	'0'..'9'
	;

protected
Decimal
	:	('0'..'9')+
	;

protected
LongSuffix
	:	'l'
	|	'L'
	;

protected
UnsignedSuffix
	:	'u'
	|	'U'
	;

protected
FloatSuffix
	:	'f'
	|	'F'
	;

protected
Exponent
	:	('e' | 'E') ('+' | '-')? (Digit)+
	;

protected
Vocabulary
	:	'\3'..'\377'
	;

Number
	:	( (Digit)+ ('.' | 'e' | 'E') )=> (Digit)+
		( '.' (Digit)* (Exponent)? {_ttype = FLOATONE;} //Zuo 3/12/01
		| Exponent                 {_ttype = FLOATTWO;} //Zuo 3/12/01
		)                          //{_ttype = DoubleDoubleConst;}
		(FloatSuffix               //{_ttype = FloatDoubleConst;}
		|LongSuffix                //{_ttype = LongDoubleConst;}
		)?

	|	("...")=> "..."            {_ttype = ELLIPSIS;}

	|	'.'                        {_ttype = DOT;}
		(	(Digit)+ (Exponent)?   {_ttype = FLOATONE;} //Zuo 3/12/01
                                   //{_ttype = DoubleDoubleConst;}
			(FloatSuffix           //{_ttype = FloatDoubleConst;}
			|LongSuffix            //{_ttype = LongDoubleConst;}
			)?
		)?

	|	'0' ('0'..'7')*            //{_ttype = IntOctalConst;}
		(LongSuffix                //{_ttype = LongOctalConst;}
		|UnsignedSuffix            //{_ttype = UnsignedOctalConst;}
		)*                         {_ttype = OCTALINT;}

	|	'1'..'9' (Digit)*          //{_ttype = IntIntConst;}
		(LongSuffix                //{_ttype = LongIntConst;}
		|UnsignedSuffix            //{_ttype = UnsignedIntConst;}
		)*                         {_ttype = DECIMALINT;}  

	|	'0' ('x' | 'X') ('a'..'f' | 'A'..'F' | Digit)+
                                   //{_ttype = IntHexConst;}
		(LongSuffix                //{_ttype = LongHexConst;}
		|UnsignedSuffix            //{_ttype = UnsignedHexConst;}
		)*                         {_ttype = HEXADECIMALINT;}   
	;

ID
	options {testLiterals = true;}
	:	( 'a'..'z' | 'A'..'Z' | '_' )
		( 'a'..'z' | 'A'..'Z' | '_' | '0'..'9' )*
	;

